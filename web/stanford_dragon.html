<!DOCTYPE html>

<html>
  <head>
    <meta charset="utf-8">
    <title>ObjViewer</title>
    <link rel="stylesheet" href="stanford_dragon.css">
  </head>
  <body>
    <h1>ObjViewer</h1>
    <canvas id="webGLCanvas" width=640px height=480px></canvas>
    <div id="sliderControls">
        <h2 class="sliders">Specular exponent</h2>
        <input id="specularExponent" type="range" min="1.0" max="200.0" step="0.1" value="30.0"/>

        <h2 class="sliders">Specular intensity</h2>
        <input id="specularIntensity" type="range" min="0.0" max="10.0" step="0.05" value="1.8"/>

        <h2 class="sliders">Roughness</h2>
        <input id="roughness" type="range" min="0.01" max="1.0" step="0.01" value="0.3"/>

        <h2 class="sliders">Diffuse intensity</h2>
        <input id="diffuseIntensity" type="range" min="0.05" max="10.0" step="0.05" value="0.6"/>

        <h2 class="sliders">Exposure</h2>
        <input id="exposure" type="range" min="0.05" max="5.0" step="0.05" value="1.5"/>
    </div>

    <script type="application/dart" src="stanford_dragon.dart"></script>
    <script src="packages/browser/dart.js"></script>
    <script id="light-vs" type=x-shader/x-vertex>
        attribute vec3 position;

        uniform mat4 cameraToClipMatrix;
        uniform mat4 modelToCameraMatrix;

        varying vec3 posCam;

        void main() {
            vec4 positionCam = modelToCameraMatrix * vec4(position, 1.0);
            gl_Position = cameraToClipMatrix * positionCam;
            posCam = positionCam.xyz;
        }
    </script>
    <script id="light-fs" type="x-shader/x-fragment">
        varying mediump vec3 posCam;

        void main() {
            gl_FragData[0] = vec4(1.0, 1.0, 1.0, 1.0);
        }
    </script>
    <script id="vertex-shader" type="x-shader/x-vertex">
        attribute vec3 position;
        attribute vec3 normal;

        uniform mat4 cameraToClipMatrix;
        uniform mat4 modelToCameraMatrix;

        varying vec3 viewPosition;
        varying vec4 lightDir;
        varying vec3 lightPos;
        varying mediump vec3 normalCam;

        void main() {
            vec4 positionCam = modelToCameraMatrix * vec4(position, 1.0);
            gl_Position = cameraToClipMatrix * positionCam;

            viewPosition = positionCam.xyz;

            lightDir = vec4(0.5, 0.7, 1.0, 0.0);
            lightDir = normalize(modelToCameraMatrix * lightDir);
            lightPos = (modelToCameraMatrix * vec4(modelToCameraMatrix[0].xyz, 1.0)).xyz;

            normalCam = (modelToCameraMatrix * vec4(normal, 0.0)).xyz;
        }
    </script>
    <script id="fragment-shader" type="x-shader/x-fragment">
        //#extension GL_OES_standard_derivatives : enable
        #extension GL_EXT_draw_buffers : require

        uniform mediump vec4 colour;

        varying highp vec3 viewPosition;
        varying mediump vec4 lightDir;
        varying mediump vec3 normalCam;

        void main() {
            // Flat
            // mediump vec3 normal = normalize(cross(dFdx(viewPosition), dFdy(viewPosition)));

            // Smooth
            mediump vec3 normal = normalize(normalCam);

            gl_FragData[0] = vec4(normal, 0);
            gl_FragData[1] = colour;
        }
    </script>
    <script id="basic-vs" type="x-shader/x-vertex">
        attribute vec2 position;
        attribute vec2 uv;

        uniform mat4 modelToCameraMatrix;
        uniform mat4 cameraToClipMatrix;
        uniform mat4 lightMatrix;

        varying mediump vec2 UV;
        varying mediump vec4 lightDir;
        varying mediump vec3 viewRay;

        void main() {
            UV = uv;
            gl_Position = vec4(position, 0, 1);

            lightDir = vec4(0.5, 0.7, 1.0, 0.0);
            lightDir = normalize(modelToCameraMatrix * lightDir);

            viewRay = vec3((UV.x * 2.0 - 1.0) / cameraToClipMatrix[0].x,
                           (UV.y * 2.0 - 1.0) / cameraToClipMatrix[1].y,
                           -1.0);
        }
    </script>
    <script id="basic-fs" type="x-shader/x-fragment">
        #extension GL_EXT_draw_buffers : require

        varying mediump vec2 UV;
        varying mediump vec4 lightDir;
        varying mediump vec3 viewRay;

        uniform highp mat4 cameraToClipMatrix;
        uniform highp mat4 lightMatrix;

        uniform mediump float specExp;
        uniform mediump float specInt;
        uniform mediump float roughness;
        uniform mediump float exposure;
        uniform mediump float diffuseInt;

        uniform sampler2D depthTex;
        uniform sampler2D normalTex;
        uniform sampler2D albedoTex;
        uniform sampler2D shadowTex;
        uniform sampler2D noiseTex;

        const int kernelSize = 16;

        const mediump float uRadius = 3.5;

        uniform mediump vec3 kernel[kernelSize];

        mediump float linearDepth(mediump vec2 texCoord) {
            const mediump float zNear = 1.1;
            const mediump float zFar = 100.0;
            mediump float depth = texture2D(depthTex, texCoord).x;
            mediump float linear = (zNear / (zFar - depth * (zFar - zNear))) * zFar;
            return linear;
        }

        mediump float getOcclusion(mediump vec3 normal) {
            mediump vec3 origin = -viewRay * linearDepth(UV);

            mediump vec3 rvec =
                texture2D(noiseTex, gl_FragCoord.xy * 0.25).xyz * 2.0 - 1.0;
            mediump vec3 tangent = normalize(rvec - normal * dot(rvec, normal));
            mediump vec3 bitangent = cross(normal, tangent);

            mediump mat3 tbn = mat3(tangent, bitangent, normal);

            mediump float occlusion = 0.0;
            for (int i = 0; i < kernelSize; i++) {
                // Get sample location
                mediump vec3 sample = tbn * kernel[i];

                sample = sample * uRadius + origin;

                // Project sample position
                mediump vec4 offset = vec4(sample, 1.0);
                offset = cameraToClipMatrix * offset;
                offset.xy /= offset.w;
                offset.xy = offset.xy * 0.5 + 0.5;

                // Get sample depth
                mediump float sampleDepth = linearDepth(offset.xy);

                mediump float boundRadius = sample.z - sampleDepth > uRadius ? 0.0 : 1.0;
                occlusion += ((sample.z >= sampleDepth) ? 1.0 : 0.0) * boundRadius;
            }
            occlusion = (occlusion / float(kernelSize));
            //occlusion = origin.z / 10.0;
            return occlusion;
        }

        mediump vec4 inShadow() {
            mediump vec3 ray = viewRay;
            mediump vec3 cameraPos = linearDepth(UV) * ray;
            mediump vec4 worldPos = lightMatrix * vec4(cameraPos, 1.0);
            worldPos /= worldPos.w;
            worldPos = worldPos * 0.5 + 0.5;
            worldPos.x /= (1024.0 / 640.0);
            worldPos.y /= (1024.0 / 480.0);
            return worldPos;
        }

        // Oren-Nayar
        mediump float calcRoughness(mediump vec4 lightDirection, mediump vec4 normal) {
            mediump float roughness = roughness;
            mediump vec4 viewDir = normalize(vec4(-viewRay, 0.0));

            mediump float nDotL = dot(lightDirection.xyz, normal.xyz);
            mediump float nDotV = dot(viewDir.xyz, normal.xyz);

            mediump float angleLN = acos(nDotL);
            mediump float angleVN = acos(nDotV);

            mediump float alpha = max(angleLN, angleVN);
            mediump float beta = min(angleLN, angleVN);
            mediump float gamma = dot(viewDir.xyz - normal.xyz * nDotV,
                               lightDirection.xyz - normal.xyz * nDotL);

            mediump float rSquared = roughness * roughness;

            mediump float A = 1.0 - 0.5 * rSquared / (rSquared + 0.57);
            mediump float B = 0.45 * rSquared / (rSquared + 0.09);
            mediump float C = sin(alpha) / tan(beta);

            mediump float L1 = max(0.0, nDotL) * (A + B * max(0.0, gamma) * C);

            return L1;
        }

        mediump vec4 calculateSpecular(mediump vec4 lightDirection,
                                       mediump vec4 normal,
                                       mediump float intensity) {
            mediump vec3 halfAngle = normalize(lightDirection.xyz - viewRay);

            mediump float blinnTerm = dot(normal.xyz, halfAngle);
            blinnTerm = clamp(blinnTerm, 0.0, 1.0);
            blinnTerm = pow(blinnTerm, specExp);

            mediump float specIntensity = intensity * blinnTerm;

            mediump vec4 specColour = specIntensity * vec4(1.0, 1.0, 1.0, 1.0);
            return specColour;
        }

        mediump float luma(mediump vec3 rgb) {
            return rgb.g * 0.7152 + rgb.r * 0.2198;
        }

        mediump float calcExposure(mediump vec4 diffuse, mediump float exposure) {
            mediump float luminance = luma(diffuse.rgb);

            mediump float brightness = 1.0 - (exp((exposure) * -luma(diffuse.xyz)));

            return brightness;
        }

        void main() {
            mediump vec4 normal = texture2D(normalTex, UV);
            mediump vec4 albedo = texture2D(albedoTex, UV);

            if (albedo.a == 0.0) discard;

            mediump float occlusion = getOcclusion(-normal.xyz);

            mediump vec4 shadowCoord = inShadow();
            const mediump float bias = 0.005;
            mediump float visibility = texture2D(shadowTex, shadowCoord.xy).x < (shadowCoord.z - bias) ? 0.0 : 1.0;
            visibility = (shadowCoord.x > 1.0 || shadowCoord.x < 0.0)
                      || (shadowCoord.y < 0.0 || shadowCoord.y > 1.0)
                        ? 1.0 : visibility;

            mediump float roughness = calcRoughness(lightDir, normal);
            mediump vec4 specular = calculateSpecular(lightDir, normal, specInt) * (visibility);

            mediump vec4 ambient = 0.3 * albedo * (1.0 - occlusion);

            mediump vec4 finalColour = albedo * roughness * diffuseInt * (visibility)
                                     + specular + ambient;

            finalColour *= calcExposure(finalColour, exposure);

            finalColour.a = 1.0;

            finalColour = pow(finalColour, vec4(1.0/2.2));

            gl_FragColor = finalColour;

        }
    </script>
    <script id="sky-vs" type="x-shader/x-vertex">
        attribute vec2 position;
        attribute vec2 uv;

        varying mediump vec2 UV;

        void main() {
            gl_Position = vec4(position, 0.0, 1.0);
            UV = uv;
        }
    </script>
    <script id="sky-fs" type="x-shader/x-fragment">
        varying mediump vec2 UV;

        mediump float luma(mediump vec3 rgb) {
            return rgb.g * 0.7152 + rgb.r * 0.2198;
        }

        mediump float exposure(mediump vec4 diffuse, mediump float exposure) {
            mediump float luminance = luma(diffuse.rgb);

            mediump float brightness = 1.0 - (exp((exposure) * -luma(diffuse.xyz)));

            return brightness;
        }

        void main() {
            const mediump float r = 21.0 / 255.0;
            const mediump float g = 50.0 / 255.0;
            const mediump float b = 90.0 / 255.0;

            const mediump float r2 = 59.0/255.0;
            const mediump float g2 = 120.0/255.0;
            const mediump float b2 = 230.0/255.0;

            mediump vec4 colour = (vec4(r, g, b, 1.0) * UV.y
                                + vec4(r2, g2, b2, 1.0) * (1.0 - UV.y)) * 6.0;

            mediump float brightness = exposure(colour, 0.5);

            gl_FragColor = vec4(colour.rgb * brightness, 1.0);

            gl_FragColor = vec4(0.2, 0.2, 0.2, 0.2);
        }
    </script>
    <script id="post-vs" type="x-shader/x-vertex">
        attribute vec2 position;
        attribute vec2 uv;

        varying mediump vec2 UV;

        uniform sampler2D tex;

        void main() {
            gl_Position = vec4(position, 0.0, 1.0);
            UV = uv;
        }
    </script>
    <script id="post-fs" type="x-shader/x-fragment">
        varying mediump vec2 UV;
        uniform mediump vec2 texelSize;

        uniform sampler2D tex;

        const mediump float FXAA_EDGE_THRESHOLD = 1.0/8.0;
        const mediump float FXAA_EDGE_THRESHOLD_MIN = 1.0/24.0;

        const mediump float FXAA_SUBPIX_TRIM = 1.0/4.0;
        const mediump float FXAA_SUBPIX_TRIM_SCALE = 1.0/(1.0 - FXAA_SUBPIX_TRIM);
        const mediump float FXAA_SUBPIX_CAP = 0.75;

        const int FXAA_SEARCH_STEPS = 16;
        const mediump float FXAA_SEARCH_THRESHOLD = 0.25;

        mediump float fxaaLuma(mediump vec3 rgb) {
            return rgb.g * (0.587 / 0.299) + rgb.r; // Experiment with values
        }

        mediump vec4 textureOffset(sampler2D tex, mediump vec2 offset) {
            return texture2D(tex, UV + (offset * texelSize));
        }

        mediump vec3 fxaa() {
            mediump vec3 rgbN = textureOffset(tex, vec2( 0, -1)).xyz;
            mediump vec3 rgbE = textureOffset(tex, vec2( 1,  0)).xyz;
            mediump vec3 rgbS = textureOffset(tex, vec2( 0,  1)).xyz;
            mediump vec3 rgbW = textureOffset(tex, vec2(-1,  0)).xyz;
            mediump vec3 rgbM = textureOffset(tex, vec2( 0,  0)).xyz;

            mediump float lumaN = fxaaLuma(rgbN);
            mediump float lumaE = fxaaLuma(rgbE);
            mediump float lumaS = fxaaLuma(rgbS);
            mediump float lumaW = fxaaLuma(rgbW);
            mediump float lumaM = fxaaLuma(rgbM);

            // Local contrast check
            mediump float lumaMin = min(lumaM, min(min(lumaE, lumaW), min(lumaN, lumaS)));
            mediump float lumaMax = max(lumaM, max(max(lumaE, lumaW), max(lumaN, lumaS)));

            mediump float range = lumaMax - lumaMin;

            if (range < max(FXAA_EDGE_THRESHOLD_MIN, lumaMax * FXAA_EDGE_THRESHOLD)) {
                return rgbM;
            }

            // Sup-pixel aliasing test
            mediump float lumaL = (lumaN + lumaE + lumaS + lumaW + lumaM) * 0.25;
            mediump float rangeL = abs(lumaL - lumaM);
            mediump float blendL = max(0.0, (rangeL / range) - FXAA_SUBPIX_TRIM)
                         * FXAA_SUBPIX_TRIM_SCALE;
            blendL = min(FXAA_SUBPIX_CAP, blendL);
            mediump vec3 rgbL = rgbN + rgbE + rgbS + rgbW + rgbM;

            mediump vec3 rgbNE = textureOffset(tex, vec2(-1,  1)).xyz;
            mediump vec3 rgbSE = textureOffset(tex, vec2( 1,  1)).xyz;
            mediump vec3 rgbSW = textureOffset(tex, vec2( 1, -1)).xyz;
            mediump vec3 rgbNW = textureOffset(tex, vec2(-1, -1)).xyz;

            rgbL += (rgbNE + rgbSE + rgbSW + rgbNW);
            rgbL *= (1.0 / 9.0);

            mediump float lumaNE = fxaaLuma(rgbNE);
            mediump float lumaSE = fxaaLuma(rgbSE);
            mediump float lumaSW = fxaaLuma(rgbSW);
            mediump float lumaNW = fxaaLuma(rgbNW);

            //Vertical/Horizontal edge test
            mediump float edgeVert =
                abs((0.25 * lumaNW) + (-0.50 * lumaN) + (0.25 * lumaNE))
              + abs((0.50 * lumaW ) + (-1.00 * lumaM) + (0.50 * lumaE ))
              + abs((0.25 * lumaSW) + (-0.50 * lumaS) + (0.25 * lumaSE));
            mediump float edgeHorz =
                abs((0.25 * lumaNW) + (-0.50 * lumaW) + (0.25 * lumaSW))
              + abs((0.50 * lumaN ) + (-1.00 * lumaM) + (0.50 * lumaS ))
              + abs((0.25 * lumaNE) + (-0.50 * lumaE) + (0.25 * lumaSE));

            bool horzSpan = edgeHorz >= edgeVert;

            mediump float lengthSign = horzSpan ? -texelSize.y : -texelSize.x;

            if (!horzSpan) lumaN = lumaW;
            if (!horzSpan) lumaS = lumaE;

            mediump float gradientN = abs(lumaN - lumaM);
            mediump float gradientS = abs(lumaS - lumaM);

            lumaN = (lumaN + lumaM) * 0.5;
            lumaS = (lumaS + lumaM) * 0.5;


            bool pairN = gradientN > gradientS;
            if (!pairN) lumaN = lumaS;
            if (!pairN) gradientN = gradientS;
            if (!pairN) lengthSign *= -1.0;

            mediump vec2 posN;
            posN.x = UV.x + (horzSpan ? 0.0 : lengthSign * 0.5);
            posN.y = UV.y + (horzSpan ? lengthSign * 0.5 : 0.0);

            gradientN *= FXAA_SEARCH_THRESHOLD;

            mediump vec2 posP = posN;
            mediump vec2 offNP = horzSpan ? vec2(texelSize.x, 0.0) : vec2(0.0, texelSize.y);

            mediump float lumaEndN = lumaN;
            mediump float lumaEndP = lumaN;

            bool doneN = false;
            bool doneP = false;

            posN -= offNP;
            posP += offNP;

            // End-of-edge search
            for (int i = 0; i < FXAA_SEARCH_STEPS; i++) {
                if (!doneN) lumaEndN = fxaaLuma(textureOffset(tex, posN).xyz);
                if (!doneP) lumaEndP = fxaaLuma(textureOffset(tex, posP).xyz);

                doneN = doneN || (abs(lumaEndN - lumaN) >= gradientN);
                doneP = doneP || (abs(lumaEndP - lumaN) >= gradientN);
                if (doneN && doneP) break;
                if (!doneN) posN -= offNP;
                if (!doneP) posP += offNP;
            }

            mediump float dstN = horzSpan ? UV.x - posN.x : UV.y - posN.y;
            mediump float dstP = horzSpan ? posP.x - UV.x : posP.y - UV.y;
            bool directionN = dstN < dstP;

            lumaEndN = directionN ? lumaEndN : lumaEndP;

            if (((lumaM - lumaN) < 0.0) == ((lumaEndN - lumaN < 0.0)))
                lengthSign = 0.0;

            mediump float spanLength = (dstP + dstN);
            dstN = directionN ? dstN : dstP;
            mediump float subPixelOffset = (0.5 + (dstN * (-1.0/spanLength))) * lengthSign;

            mediump vec3 rgbF = textureOffset(tex,
                                       vec2(horzSpan ? 0.0 : subPixelOffset,
                                            horzSpan ? subPixelOffset : 0.0)).xyz;

            // return vec3(0.0, 1.0, 1.0);

            return mix(rgbL, rgbF, blendL);
        }

        void main() {
            mediump vec3 texelAA = fxaa();
            gl_FragColor = vec4(texelAA, 1.0);
        }
    </script>
  </body>
</html>
